;;TDL program
;;Fetch all Vouchers falling under SALES Voucher Type
;;(directly or indirectly)
;;===============================================================
[Report: RTS SalesVouchers]
    Form        : RTS SVSendRemoteMsgs
    Filtered    : Yes
    Export      : Yes

[Form: RTS SVSendRemoteMsgs]
    Parts   : SYNCHeader, RTS SVSRMBody
    Local   : Field : SYNCRequest   : Set as : "Import Data"       

[Part: RTS SVSRMBody]
    XMLTag  : "BODY"
    Part    : RTS SVSRMResponse
    Vertical: Yes

[Part: RTS SVSRMResponse]
    XMLTag  : "IMPORTDATA"                  
    Part    : RTS SVSRMData
    Vertical: Yes

[Part: RTS SVSRMData]
    XMLTag  : "REQUESTDATA"
    Part    : RTS SVSRMTallyMsg
    Vertical: Yes

[Part: RTS SVSRMTallyMsg]
    Lines   : RTS SVSRMMsgs Main
    Repeat  : RTS SVSRMMsgs Main : RTS SVSendRemoteMsgs
    Scroll  : Vertical

[Line: RTS SVSRMMsgs Main]
    XMLTag          : "TALLYMESSAGE"
    XMLAttr         : "xmlns:UDF"   : "TallyUDF"
    Line            : RTS SVSRMMsgs

[Line : RTS SVSRMMsgs]
    Field           : Simple Field
    FullObject      : Yes

[Collection: RTS SVSendRemoteMsgs]
    ;;Voucher Object
    Type            : Voucher
    Filter          : IsSalesVoucher

[System: Formula]
  ;;$$IsSales is a TDL function that accepts one parameter.
  ;;It returns TRUE if it is Sales Voucher-type otherwise false
  IsSalesVoucher :  $$IsSales:$VoucherTypeName

